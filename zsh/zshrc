#----------------------------------------------------------------------------
# ~/.zshrc
# This file is read after ~/.zshenv (and maybe ~/.zprofile), but only for
# interactive shells.
#----------------------------------------------------------------------------

#----------------------------------------------------------------------------
# Aliases
#----------------------------------------------------------------------------
[[ -e ~/.aliases ]] && source ~/.aliases || true

#----------------------------------------------------------------------------
# Generic interactive shell initialization
#----------------------------------------------------------------------------
[[ -e ~/.shrc ]] && source ~/.shrc || true

#----------------------------------------------------------------------------
# Version and feature tests
#----------------------------------------------------------------------------
autoload -Uz is-at-least
function_exists () {
    local -a files
    # This expands occurrences of $1 anywhere in $fpath,
    # removing files that don't exist.
    files=(${^fpath}/$1(N))
    # Success if any files exist.
    (( ${#files} ))
}

#----------------------------------------------------------------------------
# Shell options
#----------------------------------------------------------------------------
# Changing directories
setopt auto_pushd cdable_vars
# Completion
setopt complete_in_word
# Expansion and globbing
setopt extendedglob nomatch
# History
setopt hist_ignore_dups hist_expire_dups_first \
       hist_find_no_dups hist_ignore_space hist_no_store inc_append_history
is-at-least 4.3.9 && setopt hist_fcntl_lock
# Initialisation
# Input/Output
setopt correct no_flow_control
# Job control
setopt auto_continue no_bg_nice
# Prompting
setopt prompt_subst
# Scripts and functions
# Shell emulation
# Shell state
# ZLE
setopt beep vi

#----------------------------------------------------------------------------
# History
#----------------------------------------------------------------------------
HISTFILE=~/.zhistory
HISTSIZE=10000
SAVEHIST=10000

#----------------------------------------------------------------------------
# Key bindings
#----------------------------------------------------------------------------
bindkey -M vicmd "^R" history-incremental-search-backward
bindkey -M vicmd "^S" history-incremental-search-forward
bindkey -M vicmd "_" insert-last-word

bindkey -M viins -r "^[OA" "^[OB" "^[OC" "^[OD" "^[[A" "^[[B" "^[[C" "^[[D"
bindkey "^H" backward-delete-char "^?" backward-delete-char
bindkey -M viins "^W" backward-kill-word
bindkey -M viins "^R" history-incremental-search-backward
bindkey -M viins "^S" history-incremental-search-forward
bindkey -M viins "^[." insert-last-word
bindkey -M viins "\e[Z" reverse-menu-complete

#----------------------------------------------------------------------------
# Completion
#----------------------------------------------------------------------------
zstyle ':completion:*' auto-description 'specify: %d'
zstyle ':completion:*' completer        _expand _complete _ignored _correct _approximate
zstyle ':completion:*' format           '%B%F{magenta}%d%f%b'
zstyle ':completion:*' group-name       ''
zstyle ':completion:*' last-prompt      true
zstyle ':completion:*' list-colors      ''
zstyle ':completion:*' list-prompt      '%B%F{black}%K{yellow}At %p: Hit TAB for more, or the character to insert%f%k%b'
zstyle ':completion:*' menu             select=0
zstyle ':completion:*' select-prompt    '%B%F{black}%K{yellow}Scrolling active: current selection at %p%f%k%b'
zstyle ':completion:*' verbose          true
autoload -Uz compinit
compinit

#----------------------------------------------------------------------------
# Version control info
#----------------------------------------------------------------------------
zstyle ':vcs_info:*'              actionformats '%%b%F{magenta}[%F{green}%b%F{green}|%F{red}%a%F{magenta}]'
zstyle ':vcs_info:*'              formats       '%%b%F{magenta}[%F{green}%b%F{magenta}]'
zstyle ':vcs_info:(sv[nk]|bzr):*' branchformat  '%F{red}%b%F{white}:%F{green}%r'
autoload -Uz vcs_info

#----------------------------------------------------------------------------
# Prompt
#----------------------------------------------------------------------------
[[ ! $USERNAME =~ (mfischer|mika) ]] && local P_USER="%n"
[[ -n $SSH_CONNECTION ]]             && local P_HOST="%m"
[[ -n $P_USER && -n $P_HOST ]]       && local P_AT="@"
[[ -n $P_USER || -n $P_HOST ]]       && local P_COLON=":"
                                        local P_PATH="%30<...<%~%<<"
                                        local P_VCS="\${vcs_info_msg_0_}"
                                        local P_STATUS="%(?..(%?%))"
                                        local P_CONDA="\${conda_info_msg}"
PS1="\
${P_CONDA}\
%(#.%F{red}%B.%F{green})${P_USER}\
%b%F{green}${P_AT}\
%F{cyan}${P_HOST}\
%f${P_COLON}\
%F{blue}%B${P_PATH}\
${P_VCS}\
%F{red}${P_STATUS}\
%b%f%# "

is-at-least 4.3.9 || PS1="${P_CONDA}${P_USER}${P_AT}${P_HOST}${P_COLON}${P_PATH}${P_VCS}${P_STATUS}%# "

function zle-line-init zle-keymap-select {
    RPS1="${${KEYMAP/vicmd/}/(main|viins)/-- INSERT --}"
    RPS2=$RPS1
    zle reset-prompt
}
zle -N zle-line-init
zle -N zle-keymap-select

#----------------------------------------------------------------------------
# Helper functions
#----------------------------------------------------------------------------
function settitle
{
    case $TERM in
    xterm*|rxvt*|gnome*|konsole*)
        echo -ne "\e]0;${@}\007";;
    screen*)
        echo -ne "\ek${@}\e\\";;
    esac
}
function my_preexec
{
    settitle "${1}"
}
function my_precmd
{
    local expanded=$(print -Pn "${P_USER}${P_AT}${P_HOST}${P_COLON}${P_PATH}")
    settitle "${expanded}"
}
function set_conda_info_msg
{
    if [[ -n $CONDA_DEFAULT_ENV ]]; then
        conda_info_msg="%b%F{magenta}[%F{yellow}$CONDA_DEFAULT_ENV%F{magenta}]%f"
    else
        conda_info_msg=""
    fi
}

#----------------------------------------------------------------------------
# Hooks
#----------------------------------------------------------------------------
autoload -Uz add-zsh-hook
function_exists vcs_info && add-zsh-hook precmd vcs_info
add-zsh-hook precmd set_conda_info_msg
add-zsh-hook precmd my_precmd
add-zsh-hook preexec my_preexec

#----------------------------------------------------------------------------
# Local zsh initialization
#----------------------------------------------------------------------------
[[ -e ~/.zshrc.local ]] && source ~/.zshrc.local || true

# vim: ft=zsh
